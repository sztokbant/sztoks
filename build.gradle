plugins {
    id 'java'
    id 'maven-publish'
    id 'org.springframework.boot' version '3.0.5'
    id "io.spring.dependency-management" version "1.1.0"
    id "com.diffplug.spotless" version "6.13.0"
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.google.collections:google-collections:1.0'
    implementation 'net.logstash.logback:logstash-logback-encoder:7.3'
    implementation 'org.apache.commons:commons-lang3'
    implementation 'org.apache.tomcat.embed:tomcat-embed-core'
    implementation 'org.apache.tomcat.embed:tomcat-embed-jasper'
    implementation 'org.joda:joda-money:1.0.3'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-tomcat'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'io.github.mngsk:device-detector:1.0.10'

    // JSTL
    implementation 'jakarta.servlet.jsp.jstl:jakarta.servlet.jsp.jstl-api'
    implementation 'org.glassfish.web:jakarta.servlet.jsp.jstl'

    runtimeOnly 'com.mysql:mysql-connector-j'
    testImplementation 'com.h2database:h2:1.4.200'
    testImplementation 'org.mockito:mockito-inline'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'

    // Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
description = 'demo'
java.sourceCompatibility = JavaVersion.VERSION_17

publishing {
    publications {
        maven(MavenPublication) {
            from(components.java)
        }
    }
}

jar {
    enabled = false
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

configurations.implementation {
    spotless {
        java {
            // Ref.: https://github.com/diffplug/spotless/issues/240#issuecomment-385206606
            custom 'noWildcardImports', {
                if (it.contains('*;\n')) {
                    throw new Error('No wildcard imports allowed')
                }
            }
            bumpThisNumberIfACustomStepChanges(1)

            googleJavaFormat().aosp()
        }
    }
    compileJava {
        dependsOn spotlessApply
    }
}

test {
    useJUnitPlatform()
}
